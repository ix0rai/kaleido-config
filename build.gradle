//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.github.breadmoirai:github-release:2.4.1"
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

base {
    group = 'folk.sisby'
    archivesName = 'kaleido-config'
    version = '0.2.0'
}

def baseVersion = project.version
def qc_version = '1.2.0-beta.5'

project.version = project.version + '+' + qc_version

repositories {
    mavenCentral()
    maven { url 'https://maven.quiltmc.org/repository/release'}
    mavenLocal()
}

configurations {
    shade
    compileOnly.extendsFrom shade
}

dependencies {
    shade 'org.quiltmc:quilt-config:' + qc_version
}

jar {
    archiveClassifier = 'slim'
}

shadowJar {
    archiveClassifier = ''

    configurations = [project.configurations.shade]

    relocate 'org.quiltmc.config', 'folk.sisby.kaleido.lib.quiltconfig'

    exclude 'META-INF/**'
    exclude 'LICENSE.txt'
    exclude 'org/jetbrains/**'
    exclude 'org/intellij/**'

    minimize()
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from 'src/lib/java'
}

tasks.build.dependsOn tasks.shadowJar
tasks.build.dependsOn tasks.sourcesJar

publishing {
    repositories {
        maven {
            name 'SleepingTown'
            url 'https://repo-api.sleeping.town/'
            credentials(PasswordCredentials)
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = 'kaleido-config'
            version = project.version
            artifact shadowJar
            artifact sourcesJar
        }
    }
}

if (System.getenv("GITHUB_TOKEN")) {
    apply plugin: "com.github.breadmoirai.github-release"
    githubRelease {
        token System.getenv("GITHUB_TOKEN")
        owner 'sisby-folk'
        repo "kaleido-config"
        tagName 'v' + baseVersion
        releaseName baseVersion
        targetCommitish 'main'
        draft false
        prerelease false
        releaseAssets(shadowJar.archiveFile, sourcesJar.archiveFile)
        allowUploadToExisting true
        generateReleaseNotes true
    }
    tasks.githubRelease.dependsOn(shadowJar, sourcesJar)
}